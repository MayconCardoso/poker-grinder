apply from: "$rootDir/config/android_targets.gradle"

buildscript {
    ext {
        //==========================================================================================
        // Versions - Platform
        //==========================================================================================
        GRADLE_VERSION = '7.2.2'
        KOTLIN_VERSION = '1.7.10'

        //==========================================================================================
        // Versions - Application
        //==========================================================================================
        ANDROID_CORE_VERSION = "1.8.0"
        ANDROID_APPCOMPAT_VERSION = "1.5.1"
        ANDROID_MATERIAL_VERSION = "1.6.1"
        ANDROIDX_VIEWBINDING_VERSION = "7.2.2"
        APPCOMPAT_VERSION = "1.5.1"
        LIFECYCLE_VERSION = "2.5.1"
        GSON_VERSION = "2.9.1"
        DAGGER_VERSION = "2.44"
        COROUTINES_VERSION = "1.6.4"
        ROOM_VERSION = "2.4.3"
        FRAGMENT_VERSION = "1.5.2"
        CONSTRAINT_LAYOUT_VERSION = "2.1.4"
        RECYCLER_VIEW_VERSION = "1.2.1"
        GOOGLE_NAVIGATION_VERSION = '2.5.2'
        VIEWPAGER2_VERSION = "1.0.0"

        //==========================================================================================
        // Versions - Tests
        //==========================================================================================

        JACOCO_VERSION = "0.8.7"
        JUNIT_VERSION = "4.13.2"
        MOCKK_VERSION = "1.12.8"
        ASSERTJ_VERSION = "3.23.1"
        ANDROIDX_TEST_EXT_JUNIT_VERSION = "1.1.3"
        ANDROIDX_TEST_VERSION_RUNNER = "1.4.0"
        ANDROIDX_TEST_VERSION_RULES = "1.4.0"
        ARCH_CORE_TESTING_VERSION = "2.1.0"
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$GRADLE_VERSION"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VERSION"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$DAGGER_VERSION"
        classpath "com.vanniktech:gradle-android-junit-jacoco-plugin:0.16.0"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$GOOGLE_NAVIGATION_VERSION"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    afterEvaluate { project ->
        tasks.withType(Test) {
            testLogging {
                exceptionFormat "full"
                events "passed", "failed"
                showStandardStreams true
            }
        }

        if (project.hasProperty('android')) {
            android {
                compileSdk targets.compileSdk
                defaultConfig {
                    minSdk targets.minSdk
                    targetSdk targets.targetSdk
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }
                buildTypes {
                    release {}
                    debug {
                        testCoverageEnabled false
                    }
                }
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_11
                    targetCompatibility JavaVersion.VERSION_11
                }
                kotlinOptions {
                    jvmTarget = targets.kotlinJvmTarget
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: "com.vanniktech.android.junit.jacoco"

junitJacoco {
    jacocoVersion = '0.8.7'
    excludes = [
            '**/*App.*',
            '**/*Application.*',
            '**/*Adapter.*',
            '**/*Activity.*',
            '**/*Fragment.*',
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            'android/**/*.*',
            '**/*Test*.*',
            '**/testing/**',
            '**/di/**',
            '**/*Dagger.*',
            '**/architecture_testing/agent/*.*',
    ]
}